/*****************************************************************************/
/* Linker file for LS2085A processors                                        */
/*****************************************************************************/


MEMORY
{
    /* hw_context:             org = 0x00000000,   len = 0x000000C0 --> Reserved for Hardware Context */
    /* presentation:           org = 0x00000140,   len = 0x000002C0 --> Reserved for Frame presentation area  */
    /* shared_ram_heap:        org = 0x01010000,   len = 0x00030000 --> Reserved for Shared-RAM heap */
    /* dp_ddr:                 org = 0x58000000,   len = 0x10000000 --> Reserved for DP-DDR  heap */
    tls:                    org = 0x000000C0,   len = 0x00000080 /* TLS - Task Local Storage */
    stack :                 org = 0x00000400,   len = 0x00007C00 /* Start location for Stack, Big stack for init task */
    i_ram:                  org = 0x00fe0000,   len = 0x00020000 /* I-RAM  */
    shared_ram:             org = 0x01000000,   len = 0x00010000 /* Shared-RAM */
    memory_data:            org = 0x01010000,   len = 0x500      /*Structure for memory ralated data exchage, reused for heap  */
    profile_sram:           org = 0x02030000,   len = 0x0000FFFC /* Profile SRAM */
    exception_handlers:     org = 0x58000000,   len = 0x00010000
    pseudo_rom:             org = 0x58010000,   len = 0x00100000
    init:                   org = 0x58110000,   len = 0x00100000
    internal_ram:     	    org = 0x58210000,   len = 0x00100000
    heap  :                 org = 0x58310000,   len = 0x00800000
}


SECTIONS
{
    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .exception_data   : {}
      .exception_data_bss   : {}
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram

    GROUP : {
        .itext (TEXT) ALIGN(0x1000): {}
        .itext_vle (VLECODE) ALIGN(0x1000): {
         *(.itext)
         *(.itext_vle)
         }
    } > i_ram

    GROUP : {
        .tdata (DATA) : {}
        .tbss (BSS) NO_INIT_BSS : {}
   } > tls

    GROUP : {
        .stext (TEXT) ALIGN(0x1000): {}
        .stext_vle (VLECODE) ALIGN(0x1000): {
         *(.stext)
         *(.stext_vle)
         }
        .shdata (DATA) : {}
        .shbss (BSS) NO_INIT_BSS : {}
   } > shared_ram

   GROUP : {
      .aiop_init_data (DATA) : {}
      .aiop_init_data_bss (BSS) NO_INIT_BSS : {}
    } > memory_data

   GROUP : {
       .psram_data   : {}
       .psram_bss   : {}
    } > profile_sram

}


_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_heap_addr   = ADDR(heap);
_heap_end    = ADDR(heap)+SIZEOF(heap);
_ssram_addr  = ADDR(shared_ram);
_ssram_end   = ADDR(shared_ram)+SIZEOF(shared_ram);

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

AIOP_INIT_DATA       = ADDR(memory_data);
EXCEPTION_HANDLERS   = ADDR(exception_handlers);
TLS_SECTION_END_ADDR = ADDR(tls)+SIZEOF(tls);
