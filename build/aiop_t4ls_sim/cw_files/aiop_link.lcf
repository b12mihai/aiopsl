/*****************************************************************************/
/* Linker file for LS2100A processors                                        */
/*****************************************************************************/


MEMORY
{
    hw_context:             org = 0x00000000,   len = 0x000000C0 /* Hardware Context         */
    tls:                    org = 0x000000C0,   len = 0x00000080 /* TLS - Task Local Storage */
    presentation:           org = 0x00000140,   len = 0x000001c0 /* Frame presentation area  */
    stack :                 org = 0x00000300,   len = 0x00000d00 /* Start location for Stack, defined for 8 tasks */    
    i_ram:                  org = 0x00fe0000,   len = 0x00020000 /* I-RAM  */               
    shared_ram:             org = 0x01000000,   len = 0x00040000 /* Shared-RAM */
    dp_ddr:                 org = 0x58000000,   len = 0x08000000 /* SoC-DDR  */
    exception_handlers:     org = 0x58000000,   len = 0x00002000
    pseudo_rom:             org = 0x58002000,   len = 0x0010e000
    init:                   org = 0x58100000,   len = 0x00100000
    internal_ram:     	    org = 0x58200000,   len = 0x00100000
    heap  :                 org = 0x58300000,   len = 0x00800000
    spintbl :               org = 0x58d00000,   len = 0x00100000
}


SECTIONS
{
    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram

    GROUP : {
        .receivecb_func (VLECODE) ALIGN (2048) : {} /* Temporal WA for easy EPID setup */  
        .itext (CONST) : {}
    } > i_ram

    GROUP : {
        .tdata (DATA) : {}
        .tbss (BSS) NO_INIT_BSS : {}
   } > tls

    GROUP : {
        .shdata (DATA) : {}
        .shbss (BSS) NO_INIT_BSS : {}
   } > shared_ram
}


_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_heap_addr   = ADDR(heap);
_heap_end    = ADDR(heap)+SIZEOF(heap);
_spin_table  = ADDR(spintbl);
_ssram_addr  = ADDR(shared_ram);
_ssram_end   = ADDR(shared_ram)+SIZEOF(shared_ram);

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS   = ADDR(exception_handlers);
TLS_SECTION_END_ADDR = ADDR(tls)+SIZEOF(tls);
