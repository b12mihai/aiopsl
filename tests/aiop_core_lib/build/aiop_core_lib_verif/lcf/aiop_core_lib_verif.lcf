/*****************************************************************************/
/* lcf file for T4240LS processors 											 */
/*****************************************************************************/


MEMORY
{
    hw_context:             org = 0x00000000,   len = 0x000000C0 /* Hardware Context         */
    tls:                    org = 0x000000C0,   len = 0x00000080 /* TLS - Task Local Storage */
    presentation:           org = 0x00000140,   len = 0x000001c0 /* Frame presentation area  */
    stack :                 org = 0x00000300,   len = 0x00001000 /* Start location for Stack */
    exception_handlers:     org = 0x00800000,   len = 0x00002000 /* IRAM   */
    init:                   org = 0x00802000,   len = 0x00001000
    pseudo_rom:             org = 0x00803000,   len = 0x0001D000
    private_shared_ram:     org = 0x01000000,   len = 0x00004000 /* Application global vars  */
    heap  :                 org = 0x01004000,   len = 0x0003C000 /* currently sit in shared SRAM */
    dp_ddr:                 org = 0x50000000,   len = 0x10000000 /* DP-DDR  */
    spintbl :               org = 0x50008000,   len = 0x00001000 /* DDR - heap */
    pebm  :                 org = 0x80000000,   len = 0x00600000 /* PEBM */
}


SECTIONS
{
    HWC_PRC_ADDR = 0x50;

    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
        .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > private_shared_ram
    
    GROUP : {
        .tdata (DATA):{}
        .tbss (BSS) NO_INIT_BSS :{}
   } > tls
}


_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_heap_addr   = ADDR(heap);
_heap_end    = ADDR(heap)+SIZEOF(heap);
_spin_table  = ADDR(spintbl);

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS = ADDR(exception_handlers);
