/*****************************************************************************/
/* Linker file for LS2085A processors                                        */
/*****************************************************************************/


MEMORY
{
    /* hw_context:             org = 0x00000000,   len = 0x000000C0 --> Reserved for Hardware Context */
    /* presentation:           org = 0x00000140,   len = 0x000002C0 --> Reserved for Frame presentation area  */
    /* shared_ram_heap:        org = _ssram_heap_start,   len = 0x0002fc00 --> Reserved for Shared-RAM heap */
    /* dp_ddr:                 org = 0x40000000,   len = 0x40000000 (1GB)--> Reserved for DP-DDR */
    /* sys_ddr:                org = 0x80000000,   len = 0x80000000 (2GB) --> Reserved for System DDR*/
    tls:                    org = 0x000000C0,   len = 0x0000012C /* TLS - Task Local Storage */
    stack :                 org = 0x00000480,   len = 0x00007B80 /* Start location for Stack, Big stack for init task */
    i_ram:                  org = 0x00fe0000,   len = 0x00020000 /* I-RAM  */
    memory_data:            org = 0x01000000,   len = 0x400      /* Structure for memory related data exchange, reused after boot  */
    shared_ram:             org = 0x01000400,   len = 0x00020000 /* Shared-RAM */
    profile_sram:           org = 0x02030000,   len = 0x0000FFFC /* Profile SRAM */
    exception_handlers:     org = 0x40000000,   len = 0x00010000
    pseudo_rom:             org = 0x40010000,   len = 0x00100000
    init:                   org = 0x40110000,   len = 0x00100000
    heap  :                 org = 0x40210000,   len = 0x00800000
}

SECTIONS
{
    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .exception_data   : {}
      .exception_data_bss   : {}
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .dtext (TEXT) ALIGN(0x1000): {}
        .dtext_vle (VLECODE) ALIGN(0x1000): {
         *(.dtext)
         *(.dtext_vle)
         }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
        .itext (TEXT) ALIGN(0x1000) : {}
        .itext_vle (VLECODE) ALIGN(0x1000): {
             *(.itext)
             *(.itext_vle)
         }
    } > i_ram

    GROUP : {
        .tdata (DATA) : {}
        .tbss (BSS) NO_INIT_BSS : {}
        .verif_tdata   : {}
        .verif_tbss   : {}
   } > tls

    GROUP : {
        .verif_data   : {}
        .verif_bss   : {}
        .stext (TEXT) ALIGN(0x1000): {}
        .stext_vle (VLECODE) ALIGN(0x1000): {
         *(.stext)
         *(.stext_vle)
         }

       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
   } > shared_ram

   GROUP : {
      .aiop_init_data (DATA) : {}
      .aiop_init_data_bss (BSS) NO_INIT_BSS : {}
    } > memory_data

   GROUP : {
       .psram_data   : {}
       .psram_bss   : {}
    } > profile_sram

}

_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_heap_addr   = ADDR(heap);
_heap_end    = ADDR(heap)+SIZEOF(heap);
_memory_data_addr = ADDR(memory_data); 
_ssram_addr  = ADDR(shared_ram);
_ssram_end   = ADDR(shared_ram)+SIZEOF(shared_ram);
_ssram_heap_start = _ssram_end;

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

AIOP_INIT_DATA       = ADDR(memory_data);
EXCEPTION_HANDLERS   = ADDR(exception_handlers);
TLS_SECTION_END_ADDR = ADDR(tls)+SIZEOF(tls);

/* Definitions for required space of AIOP image in DDR */
/* Used for initialization of aiop_app_init_info ddr size */
AIOP_DDR_START       = ADDR(exception_handlers);
AIOP_DDR_END         = _heap_end;
